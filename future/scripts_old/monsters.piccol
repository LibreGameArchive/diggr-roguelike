
def [a:Sym b:Monster];
def [a:[UInt UInt] b:MonsterVal];
def [a:MonsterVal b:Monster];
def [a:MonsterKey b:Sym c:UInt];


make [Sym Monster]->MonsterVal :-
  MonsterVal {species=\a}.

def {
  x: UInt
  y: UInt
  known_p: Bool
  px: UInt
  py: UInt
  fast: Bool
  range: UInt
  static: Bool
  boulder: Bool
  bld_delta: [Int Int]

  result: MonsterValXY
} WalkData;

def {
  x: UInt
  y: UInt
  heatseeking: Bool
  px: UInt
  py: UInt
  dist: Real
  range: UInt
  p_is_water: Bool

  mon: MonsterVal
} TargetData;

def [a:WalkData b:[UInt UInt]];
def [a:WalkData b:[Int Int]];
def [x:UInt y:UInt x2:UInt y2:UInt n:UInt range:UInt];
def [a:UInt b:UInt c:UInt d:UInt];
def [a:UInt b:UInt c:MonsterVal];
def [a:MonsterValXY c:Monster];



walk_path WalkData->[UInt UInt] :-
  \known_p ? [ \x \y \px \py 
               (\fast->UInt(\\ ? 2u ; 1u)) 
               (<: \range * 2u :>) ] dg_render_path_walk->[UInt UInt]
.

walk_to [ WalkData [UInt UInt] ]->WalkData :-
  \a { result=(\a->result { x=(\b->a) y=(\b->b) } ) }
.

walk_to [ WalkData [Int Int] ]->WalkData :-
  [ \a [ ([(\a->x) (\b->a)] add->UInt)
         ([(\a->y) (\b->b)] add->UInt) ] ] walk_to->WalkData
.

walk_to WalkData->MonsterValXY :-
  [ (\result->x) (\result->y) (get->Player->x) (get->Player->y) ] ->Bool( <: \a == \c && \b == \d :> ) ? 

     \result { x=\x y=\y } -> MonsterValXY(
        \\ { mon=(\\ monster_hits->MonsterVal) }
     )
  ;
  [ (\result->x) (\result->y) ] dg_render_is_valid->Bool ?
    [ (\result->x) (\result->y) ] dg_render_is_walkblock->Bool $not ? 
      \result
  ;
  \result { x=\x y=\y }
.

walk_rnd WalkData->[Int Int] :-
  \\->[Int Int](\fast ? [-2 2] ; [-1 1])->[Int Int](
      [ (\\ dg_random_range->Int) (\\ dg_random_range->Int) ])
.

walk WalkData->MonsterValXY :-
  \static ? \result ;
  \\ ->WalkData(
     \boulder ? <:[seq] print => 'Is boulder ', \x, ' ', \y, ':', \bld_delta->a, ' ', \bld_delta->b, '\n' :> 
[ \\ (\bld_delta)     ] walk_to->WalkData 
->WalkData(<:[seq] print => 'OK\n' :> \\)
;
     [ \\ (\\ walk_path->[UInt UInt]) ] walk_to->WalkData ;
     [ \\ (\\ walk_rnd->[Int Int])    ] walk_to->WalkData
  ) walk_to->MonsterValXY
.

target TargetData->MonsterVal :-

  <: \dist <= real(\range) && (!\heatseeking || !\p_is_water) :> ? 
     \mon { known_p=true known_pxy=[(\px) (\py)] }
  ;
     \mon { known_p=false }
.


make MonsterValXY->WalkData :-

  [ \\ (\mon->species get->Monster) ]->WalkData(

    WalkData{ result=(\a)
              x=(\a->x)
              y=(\a->y)
              known_p=(\a->mon->known_p)
              px=(\a->mon->known_pxy->a) 
              py=(\a->mon->known_pxy->b)
              fast=(\c->fast) 
              range=(\c->range)
              static=(\c->static)
              boulder=(\c->boulder)
              bld_delta=(\a->mon->bld_delta)
            }
   ).

make MonsterValXY->TargetData :-

  [ \\ (\mon->species get->Monster) ]->TargetData(

    TargetData{ mon=(\a->mon)
                x=(\a->x)
                y=(\a->y)
                heatseeking=(\c->heatseeking)
                range=(\c->range)
                px=(get->Player->x)
                py=(get->Player->y)

              } ->TargetData(
                \\ { dist=([ \x \y \px \py ] dg_dist->Real)
                     p_is_water=([ \px \py ] dg_grid_is_water->Bool) }
              )
  ).


walk MonsterValXY->MonsterValXY :-
  \\ make->WalkData walk->MonsterValXY.


/*** ***/


walk [ [UInt UInt] MonsterVal]->[ [UInt UInt] MonsterVal ] :-

  MonsterValXY{ x=(\a->a) y=(\a->b) mon=\b } walk->MonsterValXY -> [ [UInt UInt] MonsterVal ]( 
      [ [ (\x) (\y) ] \mon ]
    ).


is_dead MonsterVal->Bool :-
  <: \hp <= -3.0 :>.

target [ [UInt UInt] MonsterVal ]->MonsterVal :-

  MonsterValXY{ x=(\a->a) y=(\a->b) mon=\b } make->TargetData target->MonsterVal
.

fmt MonsterVal->Sym :- 
  [ \\ (\\->Monster(\species get->Monster)) ]->Sym(
     <:[seq] fmt =>
         \b->name,
         \a->Sym(<: \onfire > 0u :> ? ' (on fire)' ; '')
       => Sym :>
  ).

set_monster [UInt UInt Sym]->Void :-
  [ [\x \y] ([\v (\v get->Monster)] make->MonsterVal) ] -> Void(
    \\ set
    \a set_skin
  ).

remove_monster [ [UInt UInt] MonsterVal ]->Void :-
  [ (\b->MonsterKey(\species get->Monster make->MonsterKey)) (\b->species) 1u ] put
.

monster [UInt UInt]->Skin :-
  [\a \b] get->MonsterVal->Skin(\species get->Monster->skin).


